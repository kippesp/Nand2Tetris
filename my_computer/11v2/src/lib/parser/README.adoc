/*
clang-format off

Grammar from Figure 10.5

Program Structure:

<class>            ::= "class" <class-name> "{" {<class-var-decl>}*
                       {<subroutine-decl>}* "}"
<class-var-decl>   ::= ("static" | "field") <type> <var-name> {"," <var-name>}* ";"
<subroutine-decl>  ::= ("constructor" | "function" | "method")
                       ("void" | <type>) <subroutine-name>
                       "(" <input-parameters> ")" <subroutine-body>
<input-parameters> ::= {(<type> <var-name>) {"," <type> <var-name>}*}?
<subroutine-body>  ::= "{" {<var-decl>}* {<statement>}* "}"
<var-decl>         ::= "var" <type> <var-name> {"," <var-name>}* ";"
<type>             ::= "int" | "char" | "boolean" | <class-name>
<class-name>       ::= <identifier>
<subroutine-name>  ::= <identifier>
<var-name>         ::= <identifier>

Statements:

<statement>        ::= <let-statement> | <if-statement> | <while-statement> |
                       <do-statement> | <return-statement>
<let-statement>    ::= "let" (<scalar-var> | <array-var>) "=" <expression> ";"
<if-statement>     ::= "if" "(" <expression ")" "{" {<statement>}* "}"
                       {"else" "{" {<statement>}* "}"}?
<while-statement>  ::= "while" "(" <expression> ")" "{" {<statement>}* "}"
<do-statement>     ::= "do" <subroutine-call> ";"
<return-statement> ::= "return" {<expression>}? ";"

Expressions:

<expression>       ::= <term> {<infix-op> <term>}*
<term>             ::= <integer-constant> | <string-constant> | <keyword-constant> |
                       <scalar-var> | <array-var> | <subroutine-call> |
                       "(" <expression> ")" | <prefix-op> <term>
<subroutine-call>  ::= <call-site> "(" {<expression-list>}? ")"
<call-site>        ::= <subroutine-name> |
                       (<class-name> | <var-name>) "." <subroutine-name>
<scalar-var>       ::= <var-name>
<array-var>        ::= <var-name> "[" <expression> "]"
<expression-list>  ::= {<expression {"," <expression>}*}?
<keyword-constant> ::= "true" | "false" | "null" | "this"
<prefix-op>        ::= "-" | "~"
<infix-op>         ::= <or-op> | <and-op> | <cmp-op> | <add-op> | <mult-op>
<or-op>            ::= "|"
<and-op>           ::= "&"
<cmp-op>           ::= "<" | ">" | "="
<add-op>           ::= "+" | "-"
<mult-op>          ::= "*" | "/"

Expressions (recursive descent):

source: http://nand2tetris-questions-and-answers-forum.52.s1.nabble.com/Adding-Standard-Operator-Precedence-to-the-Jack-Compiler-td4028444.html

<expression>       ::= <or-expr>
<or-expr>          ::= <and-expr> {<or-op> <and-expr>}*
<and-expr>         ::= <cmp-expr> {<and-op> <cmp-expr>}*
<cmp-expr>         ::= <add-expr> {<cmp-op> <add-expr>}*
<add-expr>         ::= <mult-expr> {<add-op> <mult-expr>}*
<mult-expr>        ::= <term> {<mult-op> <term>}*
<term>             ::= <int-const> | <string-const> | <keyword-const> |
                       <scalar-var> | <array-var> | <subroutine-call> |
                       "(" <expression> ")" | <prefix-op> <term>
<keyword-constant> ::= "true" | "false" | "null" | "this"
<subroutine-call>  ::= <call-site> "(" {<expression-list>}? ")"
<call-site>        ::= <subroutine-name> |
                       (<class-name> | <var-name>) "." <subroutine-name>
<scalar-var>       ::= <var-name>
<array-var>        ::= <var-name> "[" <expression> "]"
<or-op>            ::= "|"
<and-op>           ::= "&"
<cmp-op>           ::= "<" | ">" | "="
<add-op>           ::= "+" | "-"
<mult-op>          ::= "*" | "/"
<prefix-op>        ::= "-" | "~"
